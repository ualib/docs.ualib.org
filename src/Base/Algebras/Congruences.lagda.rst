.. FILE      : Base/Algebras/Congruences.lagda.rst
.. AUTHOR    : William DeMeo
.. DATE      : 02 Jun 2022
.. UPDATED   : 23 Jun 2022

.. _base-algebras-congruence-relations:

Congruence Relations
~~~~~~~~~~~~~~~~~~~~

This is the `Base.Algebras.Congruences` module of the agda-algebras_ library.

::

  {-# OPTIONS --without-K --exact-split --safe #-}

  open import Overture using ( ğ“ ; ğ“¥ ; Signature )

  module Base.Algebras.Congruences {ğ‘† : Signature ğ“ ğ“¥} where

  -- Imports from Agda and the Agda Standard Library ------------------------------
  open import Agda.Primitive   using () renaming ( Set to Type )
  open import Data.Product     using ( Î£-syntax ; _,_ )
  open import Function.Base    using ( _âˆ˜_ )
  open import Level            using ( Level ; _âŠ”_ ; suc )
  open import Relation.Binary  using ( IsEquivalence ) renaming ( Rel to BinRel )
  open import Relation.Binary.PropositionalEquality using ( _â‰¡_ ; refl )

  -- Imports from agda-algebras ---------------------------------------------------
  open import Overture        using ( âˆ£_âˆ£ ; âˆ¥_âˆ¥ )
  open import Base.Relations  using ( _|:_ ; 0[_] ; 0[_]Equivalence ; _/_ ; âŸª_âŸ« ; IsBlock )
  open import Base.Equality   using ( swelldef )

  open import Base.Algebras.Basic     {ğ‘† = ğ‘†}  using ( Algebra ; compatible ; _Ì‚_ )
  open import Base.Algebras.Products  {ğ‘† = ğ‘†}  using ( ov )

  private variable Î± Î² Ï : Level

A *congruence relation* of an algebra ``ğ‘¨`` is defined to be an equivalence
relation that is compatible with the basic operations of ``ğ‘¨``. This concept
can be represented in a number of alternative but equivalent ways. Formally,
we define a record type (``IsCongruence``) to represent the property of being
a congruence, and we define a Sigma type (``Con``) to represent the type of
congruences of a given algebra.

::

  record IsCongruence (ğ‘¨ : Algebra Î±)(Î¸ : BinRel âˆ£ ğ‘¨ âˆ£ Ï) : Type(ov Ï âŠ” Î±)  where
   constructor mkcon
   field
    is-equivalence : IsEquivalence Î¸
    is-compatible  : compatible ğ‘¨ Î¸

  Con : (ğ‘¨ : Algebra Î±) â†’ Type(Î± âŠ” ov Ï)
  Con {Î±}{Ï}ğ‘¨ = Î£[ Î¸ âˆˆ ( BinRel âˆ£ ğ‘¨ âˆ£ Ï ) ] IsCongruence ğ‘¨ Î¸

Each of these types captures what it means to be a congruence and they
are equivalent in the sense that each implies the other. One implication
is the "uncurry" operation and the other is the second projection.

::

  IsCongruenceâ†’Con : {ğ‘¨ : Algebra Î±}(Î¸ : BinRel âˆ£ ğ‘¨ âˆ£ Ï) â†’ IsCongruence ğ‘¨ Î¸ â†’ Con ğ‘¨
  IsCongruenceâ†’Con Î¸ p = Î¸ , p

  Conâ†’IsCongruence : {ğ‘¨ : Algebra Î±} â†’ ((Î¸ , _) : Con{Î±}{Ï} ğ‘¨) â†’ IsCongruence ğ‘¨ Î¸
  Conâ†’IsCongruence Î¸ = âˆ¥ Î¸ âˆ¥


.. _base-algebras-example:

Example
^^^^^^^

We now defined the *zero relation* ``0[_]`` and build the *trivial congruence*,
which has ``0[_]`` as its underlying relation. Observe that ``0[_]`` is equivalent
to the identity relation ``â‰¡`` and is obviously an equivalence relation.

::

  open Level

  -- Example. The zero congruence of a structure.
  0[_]Compatible :  {Î± : Level}(ğ‘¨ : Algebra Î±){Ï : Level} â†’ swelldef ğ“¥ Î±
   â†’                (ğ‘“ : âˆ£ ğ‘† âˆ£) â†’ (ğ‘“ Ì‚ ğ‘¨) |: (0[ âˆ£ ğ‘¨ âˆ£ ]{Ï})

  0[ ğ‘¨ ]Compatible wd ğ‘“ {i}{j} ptws0  = lift Î³
   where
   Î³ : (ğ‘“ Ì‚ ğ‘¨) i â‰¡ (ğ‘“ Ì‚ ğ‘¨) j
   Î³ = wd (ğ‘“ Ì‚ ğ‘¨) i j (lower âˆ˜ ptws0)

  open IsCongruence
  0Con[_] : {Î± : Level}(ğ‘¨ : Algebra Î±){Ï : Level} â†’ swelldef ğ“¥ Î± â†’ Con{Î±}{Î± âŠ” Ï}  ğ‘¨
  0Con[ ğ‘¨ ]{Ï} wd = let 0eq = 0[ âˆ£ ğ‘¨ âˆ£ ]Equivalence{Ï}  in
   âˆ£ 0eq âˆ£ , mkcon âˆ¥ 0eq âˆ¥ (0[ ğ‘¨ ]Compatible wd)

A concrete example is ``âŸªğŸâŸ«[ ğ‘¨ â•± Î¸ ]``, presented in the next subsection.

.. _base-algebras-quotient-algebras:

Quotient algebras
^^^^^^^^^^^^^^^^^

In many areas of abstract mathematics the *quotient* of an algebra ``ğ‘¨`` with
respect to a congruence relation ``Î¸`` of ``ğ‘¨`` plays an important role. This
quotient is typically denoted by ``ğ‘¨ / Î¸`` and Agda allows us to define and
express quotients using this standard notation.

::

  _â•±_ : (ğ‘¨ : Algebra Î±) â†’ Con{Î±}{Ï} ğ‘¨ â†’ Algebra (Î± âŠ” suc Ï)

  ğ‘¨ â•± Î¸ = ( âˆ£ ğ‘¨ âˆ£ / âˆ£ Î¸ âˆ£ )  ,                            -- domain of quotient algebra
          Î» ğ‘“ ğ‘ â†’ âŸª (ğ‘“ Ì‚ ğ‘¨)(Î» i â†’  IsBlock.blk âˆ¥ ğ‘ i âˆ¥) âŸ«  -- ops of quotient algebra

**Example**. If we adopt the notation ``ğŸ[ ğ‘¨ â•± Î¸ ]`` for the zero (or identity)
relation on the quotient algebra ``ğ‘¨ â•± Î¸``, then we define the zero relation
as follows.

::

  ğŸ˜[_â•±_] : (ğ‘¨ : Algebra Î±)(Î¸ : Con{Î±}{Ï} ğ‘¨) â†’ BinRel (âˆ£ ğ‘¨ âˆ£ / âˆ£ Î¸ âˆ£)(Î± âŠ” suc Ï)
  ğŸ˜[ ğ‘¨ â•± Î¸ ] = Î» u v â†’ u â‰¡ v

From this we easily obtain the zero congruence of ``ğ‘¨ â•± Î¸``.

::

  ğŸ[_â•±_] :  {Î± : Level}(ğ‘¨ : Algebra Î±){Ï : Level}(Î¸ : Con {Î±}{Ï}ğ‘¨)
   â†’        swelldef ğ“¥ (Î± âŠ” suc Ï)  â†’ Con (ğ‘¨ â•± Î¸)

  ğŸ[_â•±_] {Î±} ğ‘¨ {Ï} Î¸ wd = let 0eq = 0[ âˆ£ ğ‘¨ â•± Î¸ âˆ£ ]Equivalence  in
   âˆ£ 0eq âˆ£ , mkcon âˆ¥ 0eq âˆ¥ (0[ ğ‘¨ â•± Î¸ ]Compatible {Ï} wd)

Finally, the following elimination rule is sometimes useful (but it "cheats" a
bit by baking in a large amount of extensionality that is miraculously true).

::

  open IsCongruence

  /-â‰¡ :  {ğ‘¨ : Algebra Î±}(Î¸ : Con{Î±}{Ï} ğ‘¨){u v : âˆ£ ğ‘¨ âˆ£}
   â†’     âŸª u âŸ« {âˆ£ Î¸ âˆ£} â‰¡ âŸª v âŸ« â†’ âˆ£ Î¸ âˆ£ u v

  /-â‰¡ Î¸ refl = IsEquivalence.refl (is-equivalence âˆ¥ Î¸ âˆ¥)

--------------
